program -> declaration* EOF

declaration -> varDecl | statement;

varDecl -> 'var' IDENTIFIER ('=' expression)? ';'

statement -> printStmt | exprStmt | blockStmt | ifStmt

ifStmt -> 'if' '(' expression ')' statement ('else' statement)?

blockStmt -> '{' declaration* '}'

printStmt -> 'print' ternary (',' ternary)* ';'

exprStmt -> expression ';'

expression -> assignment

assignment -> IDENTIFIER '=' assignment 
             | comma

comma -> ternary (',' ternary)*

ternary -> logic_or ('?' ternary ':' ternary)?

logic_or -> logic_and ('||' logic_and)*

logic_and -> equality ('&&' equality)*

equality -> comparison (('==' | '!=') comparison)*

comparison -> addition (('>' | '>=' | '<' | '<=') addition)*

addition -> multiplication (('+' | '-') multiplication)*

multiplication -> unary (('*' | '/') unary)*

unary -> (('!' | '-') unary) | primary

primary -> 'true' | 'false' | NUMBER | STRING
	| '(' expression ')' | IDENTIFIER