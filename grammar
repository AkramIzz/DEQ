program -> declaration* EOF

declaration -> varDecl | statement;

varDecl -> 'var' IDENTIFIER ('=' expression)? ';'

statement -> printStmt | exprStmt | blockStmt | ifStmt
            | whileStmt | forStmt | breakStmt | continueStmt

ifStmt -> 'if' '(' expression ')' statement ('else' statement)?

whileStmt -> 'while' '(' expression ')' statement

forStmt -> 'for' '('
                (varDecl | exprStmt | ';')
                expression? ';'
                expression? ')'
            statement

breakStmt -> 'break' ';'

continueStmt -> 'continue' ';'

blockStmt -> '{' declaration* '}'

printStmt -> 'print' ternary (',' ternary)* ';'

exprStmt -> expression ';'

expression -> comma

comma -> assignment (',' assignment)*

assignment -> IDENTIFIER '=' assignment 
             | ternary

ternary -> logic_or ('?' ternary ':' ternary)?

logic_or -> logic_and ('||' logic_and)*

logic_and -> equality ('&&' equality)*

equality -> comparison (('==' | '!=') comparison)*

comparison -> addition (('>' | '>=' | '<' | '<=') addition)*

addition -> multiplication (('+' | '-') multiplication)*

multiplication -> unary (('*' | '/') unary)*

unary -> (('!' | '-') unary) | primary

primary -> 'true' | 'false' | NUMBER | STRING
	| '(' expression ')' | IDENTIFIER